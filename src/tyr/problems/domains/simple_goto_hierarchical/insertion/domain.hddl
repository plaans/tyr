(define (domain simple_goto)
    (:requirements
        :negative-preconditions
        :hierarchy
        :typing)

    (:types position truck)

    (:predicates
        (road ?p1 ?p2 - position)
        (at ?t - truck ?p - position))

    (:task freedom
        :parameters (
            ?t - truck))

    (:task goto 
        :parameters (
            ?t - truck
            ?p - position))

    (:method m-noop-freedom
        :parameters (
            ?t - truck)
        :task (freedom ?t)
        :subtasks ())

    (:method m-freedom
        :parameters (
            ?t - truck
            ?p - position)
        :task (freedom ?t)
        :ordered-subtasks (and
            (goto ?t ?p)
            (freedom ?t)))

    (:method m-noop
        :parameters (
            ?t - truck
            ?p - position)
        :task (goto ?t ?p)
        :subtasks (noop ?t ?p))

    (:method m-rec
        :parameters (
            ?t - truck
            ?s ?i ?d - position)
        :task (goto ?t ?d)
        :ordered-subtasks (and
            (move ?t ?s ?i)
            (goto ?t ?d)))

    (:action noop
        :parameters (
            ?t - truck
            ?p - position)
        :precondition (at ?t ?p)
        :effect ())

    (:action move
        :parameters (
            ?t - truck
            ?s ?d - position)
        :precondition (and
            (at ?t ?s)
            (not (= ?s ?d))
            (road ?s ?d))
        :effect (and
            (not (at ?t ?s))
            (at ?t ?d))))